plugins {
    id 'com.android.library'
    id("maven-publish")
}

group = 'com.koai-dev'
version = '1.0.1'

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        versionName "1.0.1"
        versionCode 1
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig = true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
    lint {
        abortOnError true
        baseline file('lint-baseline.xml')
    }
}

dependencies {
    implementation("org.slf4j:slf4j-android:1.7.21")
}


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.koai-dev'
                artifactId = 'android-video-cache'
                version = '1.0.1'

                afterEvaluate {
                    from components.release
                }
            }
        }
    }
}

task createReleaseTag {
    doLast {
        def tagName = "1.0.1"
        try {
            exec {
                commandLine("git", "tag", "-a", tagName, "-m", "Release tag $tagName")
            }

            exec {
                commandLine("git", "push", "origin", tagName)
            }
        } catch (e) {
            println(e.toString())
        }
    }
}